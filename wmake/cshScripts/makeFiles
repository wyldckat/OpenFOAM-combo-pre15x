#!/bin/csh -f
#------------------------------------------------------------------------------
# =========                 |
# \\      /  F ield         | OpenFOAM: The Open Source CFD Toolbox
#  \\    /   O peration     |
#   \\  /    A nd           | Copyright (C) 1991-2005 OpenCFD Ltd.
#    \\/     M anipulation  |
#-------------------------------------------------------------------------------
# License
#     This file is part of OpenFOAM.
#
#     OpenFOAM is free software; you can redistribute it and/or modify it
#     under the terms of the GNU General Public License as published by the
#     Free Software Foundation; either version 2 of the License, or (at your
#     option) any later version.
#
#     OpenFOAM is distributed in the hope that it will be useful, but WITHOUT
#     ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
#     FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
#     for more details.
#
#     You should have received a copy of the GNU General Public License
#     along with OpenFOAM; if not, write to the Free Software Foundation,
#     Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA
#
# Script
#     makeFiles
#
# Description
#     Scan the current directory for source files and construct Make/files
#     It has to be written in the C-shell rather than the Bourne shell
#     because it uses file name manipulators.
#
#     Usage : makeFiles
#
#------------------------------------------------------------------------------

if ( -e "Make/files" ) then
    echo "makeFiles: Make/files already exists, exiting"
    exit 1
endif

set rulesPath = $WM_DIR/rules/$WM_ARCH$WM_COMPILER

rm -f Make/files

set dirs = `find . -name "*" -type d -print`

foreach dir ($dirs)

	if ($dir != ".") then
        set baseDirName=`echo $dir | sed 's%^\./%%' | $rulesPath/dirToString`
        set baseDir=`echo $dir | sed 's%^\./%%'`

        if ($baseDirName != "Make") then
            echo $baseDirName " = " $baseDir >> Make/files
        endif
    endif

end

echo >> Make/files

set files = `find . \( -name "*.[cC]" -o -name "*.java" \) -type f  -print`

foreach file ($files)

    set pathName = `echo $file:h | sed 's%^\.%%' | sed 's%^/%%' | $rulesPath/dirToString`
    set fileName = `echo $file:t`

    if ($pathName != "") then
        echo '$('$pathName")/"$fileName >> Make/files
    else
        echo $fileName >> Make/files
    endif

end

echo >> Make/files

set pwd = `pwd`
echo 'EXE = $(FOAM_APPBIN)/'$pwd:t >> Make/files


#------------------------------------------------------------------------------
