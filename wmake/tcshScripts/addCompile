#!/bin/tcsh -f
#------------------------------------------------------------------------------
# =========                 |
# \\      /  F ield         | OpenFOAM: The Open Source CFD Toolbox
#  \\    /   O peration     |
#   \\  /    A nd           | Copyright (C) 1991-2005 OpenCFD Ltd.
#    \\/     M anipulation  |
#-------------------------------------------------------------------------------
# License
#     This file is part of OpenFOAM.
#
#     OpenFOAM is free software; you can redistribute it and/or modify it
#     under the terms of the GNU General Public License as published by the
#     Free Software Foundation; either version 2 of the License, or (at your
#     option) any later version.
#
#     OpenFOAM is distributed in the hope that it will be useful, but WITHOUT
#     ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
#     FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
#     for more details.
#
#     You should have received a copy of the GNU General Public License
#     along with OpenFOAM; if not, write to the Free Software Foundation,
#     Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#
# Script
#     addCompile
#
# Description
#     Cleans up the dependency list and add the compilation statement.
#     It has to be written in the C-shell rather than the Bourne shell
#     because it uses file name manipulators.
#
#     Usage: wmkdep <fileName> | addCompile <fileName>
#
#------------------------------------------------------------------------------

set sourceName=$1:t
set objectName=$sourceName:r.o
set className=$sourceName:r.class
set sub=$1:e
set depName=$1:r.dep
set sourceDir=$1:h

if ($sourceDir == $sourceName) then
    set sourceDir="."
endif

if ($WM_PROJECT_DIR != "") then
    sed s%$WM_PROJECT_DIR%'$(WM_PROJECT_DIR)'% > $depName
else
	cat > $depName
endif

echo "" >> $depName

if ($sub == "java") then

    sed -e s%"\(.*\).class.*:"%'$(CLASSES_DIR)/'"\1.class\:"%   \
        -e s%$WM_PROJECT_DIR%'$(WM_PROJECT_DIR)'%               \
        >> $depName

    echo '$(CLASSES_DIR)/'$sourceDir/$className': $(EXE_DEP)' >> $depName
    echo '$(CLASSES_DIR)/'$sourceDir/$className':' >> $depName
    echo '	SOURCE_DIR='$sourceDir '\' >> $depName
    echo '	SOURCE='$1' ; $('$sub'toclass)' >> $depName

else

    sed -e s%".*.o.*:"%'$(OBJECTS_DIR)/'"$objectName\:"%  \
        -e s%$WM_PROJECT_DIR%'$(WM_PROJECT_DIR)'%         \
        >> $depName

    echo '$(OBJECTS_DIR)/'$objectName': $(EXE_DEP)' >> $depName
    echo '$(OBJECTS_DIR)/'$objectName':' >> $depName
    echo '	SOURCE_DIR='$sourceDir >> $depName
    echo '	SOURCE='$1' ; $('$sub'too)' >> $depName

endif


#------------------------------------------------------------------------------
