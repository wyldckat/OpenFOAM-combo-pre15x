# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#                       Sed Script to Reformat FOAM Code
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

# Remove trailing blanks
# ~~~~~~~~~~~~~~~~~~~~~~
s/ *$//

# Remove tabs
# ~~~~~~~~~~~
s/    /    /g

# Update spacing style
# ~~~~~~~~~~~~~~~~~~~~

s/^\/\* [ *]* \*\/$/\/\/ \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \/\//

s/\/\* \* .*tatic.*ata.*ember.* \*\//\/\/ \* \* \* \* \* \* \* \* \* \* \* \* \* \* Static Data Members \* \* \* \* \* \* \* \* \* \* \* \* \* \/\//

s/\/\* \* .*rivate.*ember.*unction.* \*\//\/\/ \* \* \* \* \* \* \* \* \* \* \* \* \* Private Member Functions  \* \* \* \* \* \* \* \* \* \* \* \/\//

s/\/\* \* .*rotected.*ember.*unction.* \*\//\/\/ \* \* \* \* \* \* \* \* \* \* \* \* Protected Member Functions  \* \* \* \* \* \* \* \* \* \* \* \/\//

s/\/\* \* .*onstructor.* \*\//\/\/ \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* Constructors  \* \* \* \* \* \* \* \* \* \* \* \* \* \* \/\//

s/\/\* \* .*estructor.* \*\//\/\/ \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* Destructor  \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* \/\//

s/\/\* \* [^e]*ember.*unction.* \*\//\/\/ \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* Member Functions  \* \* \* \* \* \* \* \* \* \* \* \* \* \/\//

s/\/\* \* [^e]*ember.*perator.* \*\//\/\/ \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* Member Operators  \* \* \* \* \* \* \* \* \* \* \* \* \* \/\//

s/\/\* \* .*riend.*unction.* \*\//\/\/ \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* Friend Functions  \* \* \* \* \* \* \* \* \* \* \* \* \* \/\//

s/\/\* \* .*riend.*perator.* \*\//\/\/ \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* Friend Operators  \* \* \* \* \* \* \* \* \* \* \* \* \* \/\//

s/\/\* \* .*stream.*perator.* \*\//\/\/ \* \* \* \* \* \* \* \* \* \* \* \* \* \* \* IOstream Operators  \* \* \* \* \* \* \* \* \* \* \* \* \/\//

s/^\/\*\**\/$/\/\/ \*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\*\* \/\//


# Remove Gavin's comment's
# ~~~~~~~~~~~~~~~~~~~~~~~~
/^\/\/ *==* *$/{
N
/^\/\/ *==* *\n *$/d
}


# Correct Gavin's function declaration style
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

# one line
# ~~~~~~~~
/^[a-zA-Z][a-zA-Z0-9:~&* ]* *([a-zA-Z0-9:~&* ,]*) *[const]* *$/,/^{/{
/^ *[a-zA-Z][a-zA-Z0-9:~* ]* *([a-zA-Z0-9&* ,]*) *$/{
s/,\([^ ]\)/, \1/g
s/( /(/
p
d
}
/^ *$/d
s/,\([^ ]\)/, \1/g
s/( /(/
s/^    :$/:/
}

# multi line
# ~~~~~~~~~~
/^[a-zA-Z][a-zA-Z0-9:~&* ]* *([a-zA-Z0-9:~&* ,]*[^)]*$/,/^{/{
/ *[a-zA-Z][a-zA-Z0-9:~* ]* *([a-zA-Z0-9&*() ,]*)[,]* *$/{
s/,\([^ ]\)/, \1/g
s/( /(/
p
d
}
s/ *,\([a-zA-Z][a-zA-Z0-9&* ]*\)/, *\
    \1/g
s/ *(\([a-zA-Z][a-zA-Z0-9&* ]*\)/\
(\
    \1/
s/ *\([a-zA-Z][a-zA-Z0-9&* ]*\),/    \1,/
s/ *\([a-zA-Z][a-zA-Z0-9&* ]*\))/    \1\
)/
/^ *$/d
s/,\([^ ]\)/, \1/g
s/( /(/
s/^    :$/:/
}


# Change {\n} to {}
# ~~~~~~~~~~~~~~~~~
/^{$/{
N
s/{\n}/{}/
}

s/for(/for (/
s/if(/if (/
s/while(/while (/

s/\/\/---[-]*/\/\/ -------------------------------------------------------------------------- \/\//

s/( /(/g
s/\([^ ]\) )/\1)/g
#s/,\([^ ]\)/, \1/g

s/^:   /:\
    /

/listOfSourceFiles/,/endListOfSourceFiles/{
s/ *.*linkName *\([a-zA-Z0-9.]*\) *endSourceEntry/        \1/
}

# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
#                                    The end
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
